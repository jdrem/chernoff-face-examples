buildscript {
    repositories {
        maven {
            url = uri('https://maven.eea.sk/artifactory/public/')
        }
    }
    dependencies {
        classpath 'org.gradlex:extra-java-module-info:1.8'
    }
}

plugins {
    id 'java-library'
    id 'org.gradlex.extra-java-module-info' version '1.8'
}

repositories {
    mavenLocal()
    mavenCentral()
}


extraJavaModuleInfo {
    module('org.apache.xmlgraphics:batik-awt-util','batik.awt.util') {
        exports 'org.apache.batik.ext.awt.g2d'
        requires 'java.desktop'
    }
    module('org.apache.xmlgraphics:batik-shared-resources','batik.shared.resources')
    module('org.apache.xmlgraphics:batik-util','batik.util')
    module('org.apache.xmlgraphics:batik-parser','batik.parser')
    module('xml-apis:xml-apis','xml.apis')
    module('xml-apis:xml-apis-ext', 'xml.apis.ext')
    module('org.apache.xmlgraphics:batik-css','batik.css')
    module('org.apache.xmlgraphics:batik-ext','batik.ext')
    module('org.apache.xmlgraphics:batik-i18n','batik.i18n') {
        exports 'org.apache.batik.i18n'
    }
    module('org.apache.xmlgraphics:batik-util','batik.util') {
        exports 'org.apache.batik.util'
        requires 'batik.constants'
    }
    module('org.apache.xmlgraphics:batik-xml','batik.xml')
    module('org.apache.xmlgraphics:batik-dom','batik.dom') {
        exports 'org.apache.batik.dom'
        requires 'java.xml'
        requires 'jdk.xml.dom'
        requires 'batik.i18n'
        requires 'batik.ext'
        requires 'batik.xml'
        requires 'batik.constants'
    }
    module('org.apache.xmlgraphics:batik-constants','batik.constants')
    module('commons-io:commons-io', 'org.apache.commons.io')
    module('commons-logging:commons-logging','commons.logging') {
        requires 'java.logging'
    }

    // Jars defined in dependencies and module-info
    module('org.apache.xmlgraphics:batik-svg-dom', 'batik.svg.dom') {
        exports 'org.apache.batik.dom.svg'
        requires 'batik.awt.util'
        requires 'batik.shared.resources'
        requires 'batik.util'
        requires 'batik.parser'
        requires 'batik.css'
        requires 'batik.i18n'
    }
    module('org.apache.xmlgraphics:batik-svggen','batik.svggen') {
        exports 'org.apache.batik.svggen'
        requires 'java.desktop'
        requires 'java.xml'
        requires 'jdk.xml.dom'
        requires 'batik.util'
        requires 'batik.awt.util'
        requires 'batik.constants'
    }
    module('org.apache.xmlgraphics:xmlgraphics-commons','xmlgraphics.commons') {
        exports 'org.apache.xmlgraphics.java2d'
        exports 'org.apache.xmlgraphics.java2d.ps'
        requires 'java.desktop'
        requires 'org.apache.commons.io'
        requires 'commons.logging'
    }
}

dependencies {
    implementation 'net.remgant:chernoff-faces:2.0.0'
    implementation 'org.apache.xmlgraphics:batik-svg-dom:1.17'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.17'
    implementation 'org.apache.xmlgraphics:xmlgraphics-commons:2.9'
}

group = 'net.remgant'
version = '1.0-SNAPSHOT'
description = 'chernoff-face-examples'
java.sourceCompatibility = JavaVersion.VERSION_21
